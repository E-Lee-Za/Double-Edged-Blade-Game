label raystart:
    #First Year.
    $ currentyear = 1

    "*ring ring ring*"

    "The telephone rings from another hallway."

    scene ray staring out of window with fade

    "You stare outside of your bedroom window for a while."

    "Maid" "(from hallway) Hello, this is the Pseudeland Manor."
    "Maid" "Oh, yes, I’ll let them know right away."
    "Maid" "Your Majesty, the school just called."
    
    show ray staring out of window2
    "A bird flies past."

    show ray staring out of window
    ray "(The school…?)"

    rosanna "Hello?"

    "Your mother picks up the call."

    rosanna "Yes."
    rosanna "Ah, perfect!"
    rosanna "Tuesday? Yes, I’ll inform him immediately."
    rosanna "Yes, thank you. Good evening."

    "She hangs up the call."

    show ray staring out of window3
    "The bird flies back in the other direction."

    rosanna "Raymond? Can you come here for a minute?"

    "You have been summoned."
    show ray staring out of window5
    "You walk out of the room and down the hallway, to where the phone is."

    scene ray and rosanna and phone with fade

    ray "Yes, Mother?"

    rosanna "Laurum Secondary Academy just called. The arrangements are all finalised, and your first day is on Tuesday."

    ray "Tuesday?"

    rosanna "Yes. We have everything ready, and we’ll show you how to get there on the day. It’s only a quick train ride."

    show ray and rosanna and phone2
    ray "Where was it, again?"

    rosanna "It’s in Scholite City."

    ray "But that’s outside of the region...how will I know how to navigate it? I’ve never been outside of the Pseudeland Region…"

    show ray and rosanna and phone3
    rosanna "I’m sure you’ll find your way quickly."

    show i hope so with fade
    ray "I hope so."

    #First Year. LSA. Ray meets Ms Alsudo
    
    scene black with fade

    ray "(Well, this is the place.)"

    play music "audio/first-year-prologue.mp3"

    scene lsa outside day with fade

    ray "(Laurum Secondary Academy.)"
    ray "(The most expensive private school in Scholite City.)"
    ray "(My parents say it’s also one of the best…)"
    ray "(...not that I’ve ever been to any others to compare it with.)"

    scene ray school office with fade

    ray "(Apparently, someone was supposed to meet me here.)"
    ray "(I am in the right place...right?)"
    ray "(I wonder when-)"

    "???" "Ah, you must be Raymond, our new student!"

    "You flinch a little."

    scene ruby school office with dissolve
    "You turn around to see a lady standing behind you."

    ray "Y-yes, yes, that’s me!"

    als "I must have startled you, sorry about that."
    als "I’m Ms Alsudo, and I’ll be your form tutor here."
    als "Nice to meet you, Raymond!"

    ray "It’s nice to meet you, too."
    show ruby school office2
    als "Welcome to Laurum Secondary Academy! Now, I arrived a little late, so how about we head over to your form room, and we can get you settled in?"

    "You nod silently, and follow Ms Alsudo out of the school office."

    scene courtyard2 with dissolve

    "The courtyard is a lot bigger than what you could see from the gates."

    als "So our classroom is just over in the Theatre building..."

    ray "(This place is so spacious...)"

    ray "(So this is what a proper school is like.)"
    ray "(Well, it’s very much different from the study we have at home.)"

    scene classroom door

    als "And this is where you and the rest of your class come to be registered, every morning and afternoon."

    als "How about we go and meet your new classmates?"

    ray "(Ah, that’s the thing about proper schools. There are other kids here too.)"

    #First Year. Form Room. Ray meets Elise and the students.

    scene black with dissolve
    scene blackboard with dissolve
    pause 0.5
    show ms alsudo smile
    with easeinleft

    als "Everyone, this is the new student I mentioned yesterday!"

    "Ms Alsudo looks your way."

    show ms alsudo neutral
    als "Would you like to introduce yourself?"

    show ms alsudo neutral at right
    with ease
    pause 0.5
    show ray nervous
    with easeinleft

    ray "Uh…hello, e-everyone,"
    show ray shy
    ray "Um...my name is Raymond, uh…i-it’s nice to meet you all..."
    ray "And...that’s--that’s all I have to say for now."

    scene rest of the class with dissolve

    ray "(Oh yes, very smooth indeed, you fool.)"
    ray "(That’s one way to make a first impression. Wait until they find out your full title.)"

    als "Thank you, Raymond. Now, let’s see..."
    als "You can go sit next to Elise over there at the back, okay?"

    "You look over to see a girl sitting alone in the back row. She looks up at the mention of her name."
    "You go to sit next to her. You notice she’s sketching something on a piece of paper."

    scene awkward with dissolve

    als "Right, now that’s sorted, we can start with the register."

    "The teacher starts to call the register."
    "“Catherine?” “Here, Miss.”"
    "“Catia?” “Here, Miss.”"

    ray "(A class...with other kids...with whom I should interact at some point, right?)"
    ray "(Well, there’s someone right here.)"
    ray "(What am I supposed to do? Say something?)"
    ray "(Maybe I should just...)"
    ray "Hi."
    show awkward2
    "The girl looks up, seemingly a little startled."

    elise "H-hi…"

    hide awkward2

    "She continues sketching while glancing at you occasionally."


    "“Johanna?” “Here, Miss.”"

    ray "(Hmm..should I say something else or stay quiet?)"

    menu:
        "Say something":
            ray "(What can I say to start a conversation…?)"

            show awkward2

            ray "That’s a nice drawing you’re making."

            elise "O-oh...thank you."

            ray "It looks like a pair of wings!"

            elise "Well,uh...y-yes, it is...just some plans for something…"

            ray "Oh, what are you planning?"

            elise "N-nothing important…(!)"

            show awkward

            ray "(I think I might have made her nervous...darn.)"
            
            "“Antoine?” “Here, Miss.”"

            "The two of you are silent for a while, listening to all the names of other classmates, waiting for your own."
            show awkward3
            "The girl places her pencil onto the table."

            show awkward4

            elise "So uh...what did you say your name was?"

            "“Raymond?”"

            ray "Here, Miss."

            elise "Ah, nice to meet you, Raymond. My name is-"

            "“Elise?”"

            elise "Here, Miss."

            ray "A pleasure to meet you too, Elise."
            ray "(I can see she's trying her best.)"


        "Stay quiet":
            "..."
            
            "“Johanna?” “Here, Miss.”"
            "“Antoine?” “Here, Miss.”"
            
            ray "(Alright then, Ray. Stay in this awkward silence you’ve brought upon yourself.)"

            "The two of you are silent for a while, listening to all the names of other classmates, waiting for your own."

            "“Raymond?”"
            
            ray "Here, Miss."
            ray "(Darn. I can feel the whole class looking at me.)"

            "“Elise?”"
            
            elise "Here, Miss."

            ray "(I think that’s everyone.)"

    als "Right, now that’s sorted, Maribel, can you take this to the office, please?"

    "A student stands up to take the clipboard to the office. The class resumes its chatter."

    als "Raymond? Elise? Please can you come up here for a bit?"

    "You and Elise walk over to the teacher."

    scene blackboard with dissolve
    show ms alsudo neutral at right
    pause 0.5
    
    show elise neutral with easeinleft:
        xalign 0.25

    show ray smile at left with easeinleft
    pause 0.5

    als "Elise, you volunteered yesterday to show the new student around, right?"
    als "You can do that now, if you’d like."

    elise "Yes, I think we’ll do that now, Miss."

    als "Alright, you two can go around the school, just make sure to be back here before the end of form-time."

    show ray laughter at left
    
    show elise laughter:
        xalign 0.25
    "Raymond and Elise" "Thanks, Miss!"

    "The two of you walk outside of the classroom."
    show ray at offscreenleft
    show elise at offscreenleft
    with ease
    
    scene black with dissolve
    pause 0.5
    stop music fadeout 0.5
    queue music ["audio/a-blessing-and-a-curse.mp3", "audio/blessing-piano.mp3"] loop
    scene firstyear with dissolve
    pause 2.1
    show firstyearbegins
    pause 1.5
    scene black with dissolve
    scene courtyard1 with dissolve
    show elise neutral at right
    show ray neutral at left
    with ease

    elise "So this is the Courtyard, where we all hang around outside of lesson time."
    elise "It’s usually quite easy to get to class from here, so there’s little chance of being late."

    show ray at offscreenleft
    show elise at offscreenright
    with ease

    "Click on a door to go towards it."

    call screen tourCourtyardR()

label tourArtR:
    scene artroom
    show elise neutral with easeinleft:
        xalign 0.25
    show ray neutral at left with easeinleft
    elise "This is the art studio, where our art lessons are. I come here quite often, I like drawing."
    show ray smile at left
    ray "You’re quite good at it too."
    show elise laughter:
        xalign 0.25
    elise "Why, thank you!"
    show elise neutral:
        xalign 0.25
    show mrs sketch neutral with easeinright:
        xalign 0.75
    show mr sketch smile at right with easeinright
    gary "Hello, Elise! Showing around a new student, are you?"
    show elise confident:
        xalign 0.25
    elise "Indeed I am!"
    ray "H-hi, nice to meet you!"
    show mrs sketch smile:
        xalign 0.75
    jodie "Lovely to meet you too. We’ll see you both in art class, okay?"
    show elise laughter:
        xalign 0.25
    elise "Okay, bye!"
    show ray at offscreenleft
    show elise at offscreenleft
    with ease

    scene black with dissolve

    $ seenArtR = True
    if not seenDtR:
        call screen tourCourtyardR()

    if seenDtR and seenArtR:
        scene courtyard2
        call screen tourCourtyard2R()


label tourDtR:
    scene workshop
    show ray smile with easeinleft:
        xalign 0.25  
    show elise neutral with easeinleft:
        xalign 0.5
      
    elise "These are the workshops, where we have our engineering classes, my favourite subject!"
    ray "Wow, I didn't have this while homeschooling…"

    show mr silversaw neutral at left
    with easeinleft

    silversaw "Ah, hello, Elise! Nice to see you again. Who’s your new friend?"
    show elise confident:
        xalign 0.5
    elise "Hi, Mr Silversaw! This is Raymond, he’s joining our class!"
    show ray smile:
        xalign 0.25
    ray "Uh...hello!"
    show mr silversaw laughter at left
    show elise neutral:
        xalign 0.5
    silversaw "Nice to meet you, Raymond. I think we have a workshop class today, I’m looking forward to seeing you both there."
    show mr silversaw neutral at left
    silversaw "Oh, and Elise, is your plan for your project finished?"
    show elise laughter:
        xalign 0.5
    elise "Yep! Just adding a couple more notes and it’ll be done."
    show mr silversaw laughter at left
    silversaw "Excellent!"
    show elise neutral:
        xalign 0.5
    show mr silversaw smile at left
    silversaw "Well now, I suppose you still have places to see, yes?"
    show elise laughter:
        xalign 0.5
    elise "Ah, that’s right! I’ll see you later, Sir!"

    scene black with dissolve

    $ seenDtR = True
    if not seenArtR:
        call screen tourCourtyardR()
    
    if seenDtR and seenArtR:
        scene courtyard2
        call screen tourCourtyard2R()

label tourLibraryR:
    show elise neutral at right
    show ray neutral at left
    elise "Over there's the library. If you're looking for info on basically anything, it's probably in there."
    show ray smile at left
    ray "That'll come in useful, I'm sure."

    call screen tourCourtyard2R()
    

label tourTheatreR:
    show elise neutral at right
    show ray neutral at left
    with ease
    elise "And that's the Theatre building."
    elise "There's some classrooms in there, and the theatre's upstairs..."
    show elise surprise at right
    elise "...except it's closed off for renovations right now, so I don't think we can go in there."
    show elise neutraler at right
    ray "Ah, that's too bad."
    ray "Speaking of which...should we head back to form now?"
    show elise nervous at right
    elise "...Yes, probably should."
    show ray neutral at offscreenleft
    show elise neutral at offscreenright
    with ease
    scene black with dissolve
    jump firstcontinuedR

label firstcontinuedR:
    scene blackboard with dissolve
    show ms alsudo neutral at right
      
    show elise neutral with easeinleft:
        xalign 0.25
    
    show ray smile at left with easeinleft

    als "Ah, you’re back! How was it?"
    ray "It was pretty good,"
    show ms alsudo smile at right
    als "Lovely! I hope you’ll enjoy your time here. Also, thank you, Elise, for volunteering, you've done a good job."
    show elise laughter:
        xalign 0.25
    "Elise smiles shyly."
    elise "Thank you, Miss."
    hide ray with easeoutleft
    hide elise with easeoutleft
    "Ms Alsudo turns to the rest of the class."
    show ms alsudo neutral at center with ease
    als "Alright everyone, it’s time to pack up and go to your first lessons, have a great day everyone, and I’ll see you in PM registration!"
    hide ms alsudo with easeoutleft

    #add comic-style background here- i'll draw this later

    "You and Elise collect your bags and head off to your first lesson. Your class timetables are nearly identical."
    scene black with dissolve
    "The rest of the day continues pretty normally, as you would expect."


    ###start the next scene plz
    scene courtyard1 with dissolve
    "Now it's lunchtime, and you're in the Courtyard."
    
    "Everyone’s outside at this time."
    "You sit alone at a table. Everyone else seems to have friends they’re talking to, except you."
    "You probably would have stuck with Elise, but she ran off somewhere."
    "As that thought crosses your mind, you see someone walking towards you."
    "You’ve seen this girl before, she’s in your registration group."
    
    show johanna neutral at left
    with easeinleft

    johanna "Hey, Raymond!"

    show ray shy at right
    with easeinright

    ray "H-hi...Johanna, was it?"

    show johanna laughter at left

    johanna "Yep, that’s me! Can I ask you something real quick?"

    ray "Um...okay…"

    show johanna surprise at left

    show ray neutral at right

    johanna "D'you know where Elise is? She left this behind in registration."

    "Johanna holds up the diagram of the mechanical wings Elise had been sketching."

    ray "Oh, um...I don’t know, but she said she’s usually in the art studio, or in the workshops?"

    show johanna laughter at left
    show ray smile at right

    johanna "Ah, okay, let’s go check there, then."

    hide johanna with easeoutleft
    hide ray with easeoutleft

    "The two of you go to the workshop classroom."
    scene black with dissolve
    scene workshop with dissolve
    

    "The door is already open, and you see Elise and Mr Silversaw, the engineering teacher. Elise appears to be looking for something in her bag."
    show oh look with easeinleft
    elise "I’m sure I put it in here…"

    silversaw "Oh, look, Elise, some friends have come to pay a visit!"

    "Elise looks up and sees you and Johanna."

    elise "Oh- hi!"

    hide oh look with easeoutright

    show mr silversaw neutral at right with easeinright
    show elise neutraler with easeinright:
        xalign 0.75
    show johanna neutral with easeinleft:
        xalign 0.25
    show ray neutral at left with easeinleft

    johanna "Hey, Elise! Raymond and I just came to give you this- you left it behind in form time."

    "Johanna hands Elise her sketch."
    show elise surprise:
        xalign 0.75
    elise "I thought I had left it somewhere...thanks!"
    show johanna laughter:
        xalign 0.25
    johanna "Of course! Hey, y’know, this thing you’re working on, looks really cool too."
    show elise neutraler:
        xalign 0.75
    show johanna neutral:
        xalign 0.25
    "Elise glances behind her at the various broken parts of what appeared to be a pair of mechanical wings."
    show elise nervous:
        xalign 0.75
    elise "Heh, thanks...well, if I could get it to work…"
    show elise neutral:
        xalign 0.75
    
    show ray smile at left
    ray "Ah, this is what you were sketching in class."
    show elise nervous:
        xalign 0.75
    elise "I’d tried to test this out the other day...all fell apart."
    show elise neutraler:
        xalign 0.75
    show mr silversaw laughter at right
    silversaw "But you went back to your design, made a few amendments…"
    show mr silversaw neutral at right
    elise "Yep. I think I worked out the problem with this...I hope."
    show ray neutral at left
    ray "How long have you been trying to make it for?"
    show elise surprise:
        xalign 0.75
    elise "I started this a couple months ago, I think."
    show elise neutraler:
        xalign 0.75
    silversaw "Your improved design looks a lot better, we’ll start on it on Thursday. Lunchtime is almost over, so you should get back to class now."
    show elise laughter:
        xalign 0.75
    elise "Ah, it’s that time already, see you later, Sir!"
    johanna "See you!"
    show ray smile at left
    ray "Bye!"
    show mr silversaw laughter at right
    silversaw "Good day, all of you!"
    "You, Elise and Johanna leave the workshop."
    hide ray
    hide elise
    hide johanna
    with easeoutleft

    scene black with dissolve
    "It’s now time to go home."
    scene left folder with dissolve
    "You pack up your stuff and are ready to go. Elise is looking through her bag, muttering to herself."
    elise "Ah, great, I left my folder in the workshop…"
    ray "We can quickly go and get it…"
    show it wont take long1 with dissolve
    "You look out of the window. The rain seems to have become quite heavy."
    show it wont take long2 with dissolve
    ray "...it won’t take long."
    scene courtyard1 rain with dissolve
    "The two of you run through the rain into the workshop classroom."
    scene workshop dark with dissolve
    "Looks like Mr Silversaw has already left for the day. You see a folder on one of the workbenches."

    elise "There it is!"
    scene drops papers with dissolve
    "Elise picks up her folder, at which point a strong breeze blows some loose papers out from it and scatters them on the floor, some sliding underneath the tool cupboard door."
    "You both pick up the pages scattered around, but when you get the one in the cupboard…"
    show black with dissolve
    stop music fadeout 1
    pause 1
    show door closes1
    pause 1
    show door closes2
    "The strong wind blows again, and slams the cupboard door shut, with both of you inside."

    pause 0.5

    play music "audio/brushed-under-the-carpet.mp3"

    elise "What was that-what was that?!"
    show black
    "You try to open the door. It’s locked, and doesn’t open from inside."
    scene help with dissolve
    ray "Is anyone out there?! We’re trapped!"

    elise "Oh, Raymond, what do we do?"
    scene black with dissolve
    elise "The lights have been broken for a while...and it's dark..."
    scene trapped1 with dissolve
    ray "Well, in that case, I have just the thing for this situation…"
    
    "You pull out a box of matches from your pocket."

    elise "Why...why do you have those??"

    ray "You never know when you’ll need them."

    elise "You’re not going to start a fire, are you?"
    show trapped2 with dissolve
    "You light the match."

    ray "Only a tiny one."

    "The two of you sit down, watching the flickering flame of the match."

    ray "Well, now...worst case scenario, perhaps Mr Silversaw will find us in the morning?"

    elise "He teaches part-time, so he won’t be in tomorrow."

    ray "Ah. There goes that idea."
    ray "Hmm, oh, well...."

    "You sit together in silence."
    "…"

    menu:
        "Try to have a conversation":
            ray "There are...a lot of tools in here. And they all look...sharp. "
            elise "They are."
            ray "Have you used any of these before?"
            elise "A few times, for a few projects."
            ray "Like your wings?"
            elise "Yep."
        
        "Say nothing":
            "..."
            "You continue to sit in silence, watching the match."
            "Elise looks a bit uneasy."
    show trapped3
    "The match goes out."
    "You’re in darkness for a few moments."
    pause 1
    show trapped4
    pause 0.5
    scene trapped1
    
    elise "AHH-! What was- what was-?!"

    ray "It’s just the storm outside. It’s alright, don’t worry-"

    elise "I’m scared, Raymond! What if something happens to us in here?!  What if nobody finds us? What if we don’t get out?"
    elise "I...I don't..."
    scene black with dissolve
    "Amongst Elise’s panicked shouting, you notice a blue glow filling the room."
    "The tools shake around before flying off of their hooks."
    show elisemagic1 at fading
    ray "Elise, please, calm down!"
    ray "What is happening here?!"
    show elisemagic2 at fading
    "She looks up to see numerous sharp objects circled around her."
    elise "What’s that?! What’s happening?!"
    show black at fading
    show he panic1 at fading

    ray "Are you doing this?"
    elise "I don’t know!"

    scene he panic2 with dissolve
    ray "Can we put them down?"
    show black at fading
    "As soon as you say that, a red glow appears to counter the blue, dropping all of the tools onto the floor."
    scene raymagic with dissolve
    elise "I mean, that works?"
    ray "Was that me?"

    elise "We can try that again…"

    show black at fading
    show tryitray at fading
    "You gesture your hand upwards. A small blade appears in front of you, and flies forward into the wall."

    ray "Wow…"
    show tryitelise at fading
    "Elise does the same, but her blade flies backwards, almost hitting her."

    ray "We’d better be careful."
    
    elise "What do we do with this knowledge?"

    scene cupboard with dissolve

    ray "Perhaps...we can find a way out."

    "Try clicking some of the tools, to find a way out."

    call screen cupboardR()

label screwdriverR:
    "You try to pick the lock open."
    "It doesn't work."
    "The door is quite stuck in its place."
    call screen cupboardR()

#label lightswitchR:
#    "The light switch is broken."
#    call screen cupboardR()

label escapecupboardR:
    "You notice the knife lodged into the wall, and an idea pops into your head."
    
    ray "What we tried, just now, with the knives…"
    "What if we tried that again?"
    scene breakout1 with dissolve
    elise "Is this safe?"

    ray "It’s about as safe as freezing to death in a cupboard during a storm."

    show breakout2 at fading
    pause 1
    show black
    pause 2
    scene breakout3 with dissolve
    pause 3
    scene breakout4 with dissolve

    show elise surprise at right
    show ray neutral at left
    with ease

    elise "Huh, that worked! That actually worked!"
    show ray laughter at left
    ray "No more freezing to death in a cupboard during a storm!"
    show ray neutral at left
    show elise neutraler at right
    "You look down at the pile of broken wood, which was once the door."

    ray "Except...what do we do with that?"
    show elise surprise at right
    elise "We can explain that later. Let’s get out of here."
    show ray at offscreenleft
    show elise at offscreenright
    with ease
    scene black with dissolve
    "You both run through the rain, to the school gate. It’s now dark outside."
    scene lsa outside night

    ray "Well...that was interesting."

    elise "Hahaha! Well, not to worry, this won’t be a regular occurrence, I’m sure."
    ray "Yes, definitely."
    elise "Well, we’d better get going, before our parents worry too much."
    elise "See you tomorrow!"
    ray "See you tomorrow, Elise."

    scene black with dissolve
    pause 1

    "You arrive home."
    scene pseudelandmanorwallpaper with dissolve
    rosanna "Raymond! Where have you been?! I was so worried about you! Are you alright??"
    show ray nervous2 with easeinleft
    ray "I’m okay, Mother, it’s just that…"
    show ray ohheck
    ray "(Ah, darn. I can’t tell her about what happened. She’ll worry.)"
    show ray awkward
    ray "...that I got a little lost on the train coming home. Yes."
    ray "But I know the way, now. It won’t happen again."
    stop music fadeout 1
    scene black with dissolve
    "Later that night..."
    ray "(Huh...)"
    ray "(What if I try this...again...)"
    scene tryagain with dissolve
    pause 1
    show donttryagain with vpunch
    pause 1
    "Don't."
    "Something about it can't be right."
    "It's dangerous."

    scene black with dissolve
    scene tryagain with dissolve
    "By now you've probably noticed the number in the corner of your screen, labelled 'Control'."
    "The higher it is, the more control you have over your powers, and vice-versa."
    "It increases when you feel calmer, and decreases when you aren't."
    "If it hits zero, you’ve completely lost control for good, and the game is over."
    "Keep it up and you’ll survive. Good luck."

    scene black with dissolve

    queue music ["audio/blessing-piano.mp3", "audio/a-blessing-and-a-curse.mp3"] loop

    "It's the last week of school and today, there’s a school trip to the History Museum in the city."
    scene museum with dissolve
    show ms alsudo neutral with easeinright
    als "Okay, everyone, here we are, Scholite University History Museum!"
    als "In here you’ll find a lot to learn about the Regions on this island, and about the city, too."
    als "Now, we’ll split into two groups and you can explore around a bit."
    als "Let’s see…"
    hide ms alsudo with easeoutleft

    show ray neutral at left with easeinright
    als "Raymond..."
    show catherine neutral with easeinright:
        xalign 0.25
    als "Catherine..."
    show maribel neutral with easeinright
    als "Maribel..."
    show fritz neutral with easeinright:
        xalign 0.75
    als "and Fritz..."
    als "That's one group, and..."

    hide ray
    hide catherine
    hide maribel
    hide fritz
    with easeoutleft

    show elise neutral at right with easeinleft
    als "Elise..."
    show catia neutral with easeinleft:
        xalign 0.75
    als "Catia..."
    show antoine neutral with easeinleft
    als "Antoine..."
    show johanna neutral with easeinleft:
        xalign 0.25
    als "and Johanna..."
    als "In the other group."

    hide elise
    hide catia
    hide antoine
    hide johanna
    with easeoutright

    show ms alsudo smile with easeinright

    als "Alright, everyone, you’re free to look around, and we’ll meet back here at two o’clock."
    als "Enjoy!"

    hide ms alsudo with easeoutleft

    "Try clicking on the displays."

    call screen museumR()

label royalportraitsR:
    scene royalportraits with dissolve

    pause 3

    show ray neutral with easeinleft:
        xalign 0.25
    show catherine neutral at left with easeinleft
    show maribel neutral with easeinright:
        xalign 0.75
    show fritz neutral at right with easeinright

    show maribel surprise:
        xalign 0.75
    maribel "Ooh, look, portrait gallery!"
    show maribel neutral:
        xalign 0.75
    catherine "I feel like I’ve seen some of these people before…"
    "You observe some of the pictures on the wall."
    show ray confused:
        xalign 0.25
    ray "Is that…?"
    show fritz thoughtful at right
    fritz "Pseudeland’s family."
    show ray awkward:
        xalign 0.25
    ray "My parents...?"
    show ray neutral:
        xalign 0.25
    show maribel smile:
        xalign 0.75
    show fritz neutral at right
    maribel "I thought they kind of looked ike you."
    show maribel neutral:
        xalign 0.75
    catherine "Over here, there's another one."
    "You look over to see a portrait of the Queen of Realis."
    show catherine neutral at left
    catherine "I hear their queen’s a bit of a scary ruler…"
    show ray confused:
        xalign 0.25
    ray "I don’t see any paintings from the Etceter region anywhere…"
    show maribel surprise:
        xalign 0.75
    maribel "Apparently they're just super secretive?"
    show ray neutral:
        xalign 0.25
    ray "Hmm…"

    scene black with dissolve
    scene museum with dissolve

    call screen museumR()

label mapofscholiteR:
    scene mapofscholite with dissolve

    show ray neutral with easeinleft:
        xalign 0.25
    show catherine neutral at left with easeinleft
    show maribel neutral with easeinright:
        xalign 0.75
    show fritz neutral at right with easeinright

    ray "The entire Isle of Scholite…"
    show ray awkward:
        xalign 0.25
    ray "Strange, I had never been out of the Pseudeland region before moving to the school."
    show ray neutral:
        xalign 0.25
    show catherine neutral at left
    catherine "I can see Laurum Secondary on the map."
    show maribel surprise:
        xalign 0.75
    maribel "I wonder if I can find my home on here…"
    show maribel neutral:
        xalign 0.75
    ray "Scholite City is a lot bigger than I thought."
    ray "Maybe one day I’ll explore around some more."

    scene black with dissolve
    scene museum with dissolve

    call screen museumR()

label sworddisplayR:
    scene sworddisplay with dissolve

    show ray neutral with easeinleft:
        xalign 0.25
    show catherine neutral at left with easeinleft
    show maribel neutral with easeinright:
        xalign 0.75
    show fritz neutral at right with easeinright

    show maribel surprise:
        xalign 0.75
    maribel "So fancy...and shiny!"

    ray "…"
    
    ray "Uh…"
    $ control -= 10

    show fritz raised eyebrows at right
    fritz "A problem?"
    show ray nervous2:
        xalign 0.25
    ray "No, no, not at all…"
    show ray ohheck:
        xalign 0.25
    ray "(Darn. If something happens here...)"
    ray "(Turn away now, turn away now...)"
    show ray awkward:
        xalign 0.25
    ray "Uh...Oh, look at the time!"
    "The clock has turned to two o’clock."
    ray "We should be going back now."

    scene black with dissolve
    scene sworddisplay2 with dissolve

    "You can’t help but notice a faint red glow inside of the display."

    scene black with dissolve
    scene museum with dissolve

    show ms alsudo smile with easeinright
    als "Alright, everyone, looks like you all found that interesting!"
    show ms alsudo neutral
    als "However, we’ll have to get going quickly, before we miss the train home."
    scene black with dissolve
    scene street with dissolve
    "You all walk out of the building and along the road to the train station."
    "You walk a little bit behind the group."
    "…"
    "Was that glass breaking?"
    ray "Hm? What is…"
    show sword at left with easeinright:
        yalign 0.2
    ray "Oh, goodness- Uh…"
    ray "You’re not supposed to be out of your case! Ah...uh…"
    ray "Go back- get back to the museum, now."
    "You gesture upwards, attempting to send it back to its proper place."
    hide sword with easeoutright
    "The sword moves, seemingly reluctantly, towards the museum entrance, before dropping onto the ground again."
    ray "Eh...close enough."

    "The group has gone ahead, and you run to catch up."
    scene trainstation day with dissolve
    "The class arrives at the train station. The train has already arrived."

    "You’re about to board the train when a blue glint catches the corner of your eye."
    "You instantly know who it is. And she’s running right towards you. From what, you don’t know."

    elise "aaaaaaaaaaaaaaaAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA-"
    show elise panic at run
    pause 0.2
    show sword at flyfastleft
    pause 0.2
    show sword at flyfastleft
    pause 0.2
    show sword at flyfastleft

    "She runs past you, and ducks down to let three copies of that same sword zoom past her head."
    "She reaches her hand out, stopping the blades in an instant."

    show ray confused at right with easeinright
    ray "What happened??"
    show elise surprise at left with easeinleft
    elise "I don’t know, I looked behind me after you went ahead with the group, and there were three swords rushing at me with full speed!"
    show ray ohheck at right
    ray "Oh, goodness…"
    show ray neutral at right
    show elise neutraler at left
    elise "We should put these back where they belong."

    show sword at flyfastright

    show elise nervous at left
    elise "There, that should do it."
    show ray ohheck at right
    ray "Uh....where did everyone else go?"
    show elise neutraler at left
    show ray neutral at right
    "You notice that the train has left without you."
    show elise panic at left
    elise "We lost the group?! We lost the group!"
    show ray ohheck at right
    ray "Well, darn. How do we get back?"
    elise "Use the train??"
    ray "Yes, but..."
    ray "...which side of the platform did they get on?"
    elise "Uhh....there's a map? Do you know how to read train maps?"
    ray "Nope."
    show elise neutraler at left
    elise "I...suppose we’ll have to guess."
    elise "Eastbound or westbound train?"

    menu: 
        "Eastbound train":
            ray "Try the eastbound train?"
            scene train day with dissolve
            "You both board the train."
            "Now we wait."
            
            "The next station comes, and you get off the train."
            scene trainstation evening with dissolve
        
            elise "There they are!"
                
            "They’re near the platform."
                
            als "One, two, three, four, five, six…"

            "Ms Alsudo looks your way."

            als "...seven, eight. Alright, that’s everyone."

            ray "(That was almost a disaster...)"

        "Westbound train":
            
            ray "Try the westbound train?"
            
            "You both board the train."
            scene train day with dissolve
            "Now we wait."
            "The next station comes, and you get off the train."
            scene trainstation evening with dissolve
            elise "Uh….where are we?"

            ray "No sign of anyone here at all."
            
            elise "Try the other way?"

            "You board the other train."

            scene train evening with dissolve

            "The train is taking a long time."

            "You look outside. The city is unfamiliar."
            
            ray " Have you ever been here before?"

            elise "Nope."

            "In an unfamiliar place like this, you start to feel a little uneasy."

            $ control -= 5
        
            "The view outside now looks different."

            "The train is taking a familiar amount of time."

            "You look outside."

            ray "Wait, I usually pass by this station when coming back from school!"

            elise "You know the way from here?"

            ray "It's this station."

            "You leave the train and go upstairs to the station."
            scene trainstation evening with dissolve
            "The class is there."

            show ms alsudo surprise at right with easeinright
            als "You’re here! Where did you both disappear to??"
            show elise nervous with easeinleft:
                xalign 0.25

            show ray awkward at left with easeinleft
            elise "Uh...we kinda missed the train and had to catch up…"
            show ms alsudo neutral at right
            als "Well, as long as you’re here now, it’s alright."

            scene black with dissolve

    "The group returns to the school."

    scene lsa outside night with dissolve
    show elise neutral at right
    show ray smile at left
    with ease

    elise "So...last day of school in a couple more days."
    elise "Will you be in for that?"
    show ray neutral at left
    ray "Probably not...my parents are going out of the Isle for a week, and they don’t want me going out while they’re not home."
    show elise nervous at right
    elise "Ah...oh, well."
    show elise neutral at right
    elise "I suppose I’ll see you in September?"
    show ray smile at left
    ray "Indeed."
    show ray laughter at left
    ray "And, if you’re travelling near the Pseudeland region, maybe you’ll find me, hahah!"
    show elise confident at right
    elise "Ahah, maybe I will!"
    show elise laughter at right
    show ray smile at left
    elise "Well, anyway...bye for now."
    show elise neutral at right
    elise "And...stay safe."
    ray "You too, Elise."

    hide elise with easeoutright

    show ray neutral with ease
    ray "(Stay safe?)"
    ray "(I’ll try to…)"

    stop music fadeout 1

    scene black with dissolve
    ray "(If I can.)"
    
    "..."

    "???" "Agh...my head..."
    "???" "it hurts..."
    "???" "...so much..."
    "??? (2)" "Increase the input level. No result has shown yet."
    "???" "Please...don't..."
    "??? (2)" "It's for the good of our Region."
    "???" "The other Regions are right about us..."
    "???" "iT HURTS-!"
    "???" "STOP! THEIR THEORY WAS INCORRECT! THE STRANGE HAPPENINGS DON'T EXIST!"
    "??? (2)" "What?"
    "???" "Why...would you believe...the Scholite researchers...?"
    "??? (3)" "The King's agents have found the facility."
    "??? (3)" "Stop the research. Leave."

    show end first ray1 at fading

    ray "(The hell was that??)"
    ray "(What kind of dream was that??)"
    ray "(Just a dream, eh...?)"
    show end first ray2
    ray "(Right. Just a dream.)"
    ray "(Go back to sleep.)"

    scene black with dissolve

    "End: First Year."

    $ currentyear = 1.5

    jump devmessage